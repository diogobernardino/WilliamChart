plugins {
    id("com.android.application")
    id("kotlin-android")
    id("kotlin-android-extensions")
    id("com.github.triplet.play") version "2.0.0"
}

Properties properties = new Properties()
File localPropertiesFiles = project.rootProject.file('local.properties')
if (localPropertiesFiles.exists())
    properties.load(localPropertiesFiles.newDataInputStream())

def storeFileProperty = properties.getProperty("storeFile")
def storePasswordProperty = properties.getProperty("storePassword")
def keyAliasProperty = properties.getProperty("keyAlias")
def keyPasswordProperty = properties.getProperty("keyPassword")

def validSigningConfig = storeFileProperty && storePasswordProperty && keyAliasProperty && keyPasswordProperty

android {

    compileSdkVersion rootProject.targetSdkVersion

    defaultConfig {
        applicationId "com.db.williamchartdemo"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 20
        versionName rootProject.williamchartVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    if (validSigningConfig) {
        signingConfigs {
            release {
                storeFile file(storeFileProperty)
                storePassword storePasswordProperty
                keyAlias keyAliasProperty
                keyPassword keyPasswordProperty
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig validSigningConfig ? signingConfigs.release : debug.signingConfig
        }
    }
}

dependencies {
    implementation project(path: ':williamchart')
    implementation project(path: ':slidertooltip')
    //implementation "com.diogobernardino:williamchart:$rootProject.williamchartVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlinVersion"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
}

play {
    serviceAccountEmail = properties.containsKey("play.accountEmail") ? properties.getProperty("play.accountEmail") : "dummyPlayAccountEmail"
    serviceAccountCredentials = file(properties.containsKey("play.p12") ? properties.getProperty("play.p12") : "dummyPlayP12")
    track = 'alpha'
}
